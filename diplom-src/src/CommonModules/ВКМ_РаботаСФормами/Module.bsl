#Область ПрограммныйИнтерфейс 

Процедура ВКМ_ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента" Тогда 
			
		ВКМ_ДобавитьАбоненскоеОбслуживание(Форма);  
		
	ИначеЕсли  ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		
		ВКМ_ДобавитьКнопкуЗаполнить(Форма);
				
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВКМ_ДобавитьАбоненскоеОбслуживание(Форма) 
	
	ГруппаАбоненскоеОбслуживание = Форма.Элементы.Добавить("ГруппаАбоненскоеОбслуживание", Тип("ГруппаФормы"));
	ГруппаАбоненскоеОбслуживание.Вид = ВидГруппыФормы.ОбычнаяГруппа; 
	ГруппаАбоненскоеОбслуживание.ОтображатьЗаголовок = Ложь;
	ГруппаАбоненскоеОбслуживание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ГруппаПериодДействия = Форма.Элементы.Добавить("ГруппаПериодДействия", Тип("ГруппаФормы"), Форма.Элементы.ГруппаАбоненскоеОбслуживание);
	ГруппаПериодДействия.Вид = ВидГруппыФормы.ОбычнаяГруппа; 
	ГруппаПериодДействия.Заголовок = "Период действия договора";
	ГруппаПериодДействия.ОтображатьЗаголовок = Истина;
	ГруппаПериодДействия.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("ВКМ_НачалаДействия", Тип("ПолеФормы"), Форма.Элементы.ГруппаПериодДействия);
	ПолеВвода.Заголовок = "с";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.Подсказка = "Дата начала действия договора";
	ПолеВвода.ПутьКДанным = "Объект.ВКМ_НачалаДействия";
	
	ПолеВвода = Форма.Элементы.Добавить("ВКМ_ОкончанияДействия", Тип("ПолеФормы"), Форма.Элементы.ГруппаПериодДействия);
	ПолеВвода.Заголовок = "по";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода; 
	ПолеВвода.Подсказка = "Дата окончания действия договора";
	ПолеВвода.ПутьКДанным = "Объект.ВКМ_ОкончанияДействия";
	
	ГруппаЦены = Форма.Элементы.Добавить("ГруппаЦены", Тип("ГруппаФормы"), Форма.Элементы.ГруппаАбоненскоеОбслуживание);
	ГруппаЦены.Вид = ВидГруппыФормы.ОбычнаяГруппа; 
	ГруппаЦены.Заголовок = "Стоимость обслуживания";
	ГруппаЦены.ОтображатьЗаголовок = Истина;
	ГруппаЦены.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ПолеВвода = Форма.Элементы.Добавить("ВКМ_СуммаАбоненсткойПлаты", Тип("ПолеФормы"), Форма.Элементы.ГруппаЦены);
	ПолеВвода.Заголовок = "Сумма абоненсткой платы";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.Подсказка = "Сумма ежемесячной абоненсткой платы";
	ПолеВвода.ПутьКДанным = "Объект.ВКМ_СуммаАбоненсткойПлаты";
	
	ПолеВвода = Форма.Элементы.Добавить("ВКМ_ЦенаЧасаРаботы", Тип("ПолеФормы"), Форма.Элементы.ГруппаЦены);
	ПолеВвода.Заголовок = "Стоимость часа работы";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.Подсказка = "Стоимость часа работы специалиста";
	ПолеВвода.ПутьКДанным = "Объект.ВКМ_ЦенаЧасаРаботы";
	
КонецПроцедуры   

Процедура ВКМ_ДобавитьКнопкуЗаполнить(Форма)

	Команда = Форма.Команды.Добавить("ВКМ_КомандаЗаполнить");
	Команда.Действие = "ВКМ_ДействиеЗаполнить";
	
	Кнопка = Форма.Элементы.Добавить("ВКМ_КнопкаЗаполнить", Тип("КнопкаФормы"), Форма.Элементы.УслугиКоманднаяПанель);
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	Кнопка.ИмяКоманды = "ВКМ_КомандаЗаполнить";
	Кнопка.Заголовок = "Заполнить";
	
КонецПроцедуры

#КонецОбласти