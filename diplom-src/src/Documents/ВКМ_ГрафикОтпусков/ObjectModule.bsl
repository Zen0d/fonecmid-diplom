#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) 

	Для каждого Стр Из ОтпускаСотрудников Цикл   
		
		Если ЗначениеЗаполнено(Стр.ДатаНачала) И ЗначениеЗаполнено(Стр.ДатаОкончания) Тогда 
			Если Год(Стр.ДатаНачала) <> Год(ГодПериод) Или Год(Стр.ДатаОкончания) <> Год(ГодПериод) Тогда
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Строка №%1 год документа отличается от года в строке.", Стр.НомерСтроки));
				Отказ = Истина;
			КонецЕсли;
			
			Если Стр.ДатаНачала > Стр.ДатаОкончания Тогда  
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Строка №%1 не верно заполнен период", Стр.НомерСтроки)); 
				Отказ = Истина; 
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецЦикла;  

КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Дубль = НаличееДубльДокумента();
	Если Дубль Тогда
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон("План графиков за %1 год уже создан",  XMLСтрока(Год(ГодПериод))));
		Отказ = Истина;
	КонецЕсли;
	
	Движения.ВКМ_ГрафикиОтпусков.Очистить();
	Движения.ВКМ_ГрафикиОтпусков.Записать();
	Движения.ВКМ_ГрафикиОтпусков.Записывать = Истина;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник КАК Сотрудник,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала КАК ДатаНачала,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания КАК ДатаОкончания,
		|	РАЗНОСТЬДАТ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала, ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания, ДЕНЬ) + 1 КАК КолвоДней,
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
		|ГДЕ
		|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
		|ИТОГИ
		|	СУММА(КолвоДней),
		|	МИНИМУМ(НомерСтроки)
		|ПО
		|	Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ВыборкаОбщееКолвоДней = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ДопустимоеКолвоДней = 28;
	
	Пока ВыборкаОбщееКолвоДней.Следующий() Цикл
		
		ПревышеноДней = ВыборкаОбщееКолвоДней.КолвоДней - ДопустимоеКолвоДней;
		
		Если ПревышеноДней > 0 Тогда     
			
			Отказ = Истина;
			ШаблонСообщения = "Привышено кол-во дней отпуска в год. Допустим %1 дн. По графику %2 дн. Превышено на %3 дн.";
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(ШаблонСообщения, ДопустимоеКолвоДней, ВыборкаОбщееКолвоДней.КолвоДней, ПревышеноДней);  
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Поле = СтрШаблон("ОтпускаСотруднеков[%1].Сотрудник", XMLСтрока(ВыборкаОбщееКолвоДней.НомерСтроки-1));
			Сообщение.Сообщить();
			
			
		КонецЕсли; 
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;    
		
		Выборка = ВыборкаОбщееКолвоДней.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Движение = Движения.ВКМ_ГрафикиОтпусков.Добавить();
			Движение.Период = Выборка.ДатаОкончания;
			Движение.Сотрудник = Выборка.Сотрудник;
			Движение.ДатаНачало = Выборка.ДатаНачала;
			Движение.ДатаОкончания = Выборка.ДатаОкончания;
			Движение.Значение = Выборка.КолвоДней;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НаличееДубльДокумента()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ГрафикОтпусков.ГодПериод КАК ГодПериод
		|ИЗ
		|	Документ.ВКМ_ГрафикОтпусков КАК ВКМ_ГрафикОтпусков
		|ГДЕ
		|	ВКМ_ГрафикОтпусков.ГодПериод = &ГодПериод
		|	И ВКМ_ГрафикОтпусков.Проведен = ИСТИНА
		|	И ВКМ_ГрафикОтпусков.Ссылка <> &Ссылка"; 
	
	Запрос.УстановитьПараметр("ГодПериод", ГодПериод);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
